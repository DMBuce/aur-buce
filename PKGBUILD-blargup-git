# Maintainer: Buce <dmbuce@gmail.com>

pkgname=blargup-git
pkgver=0.6.1792298
pkgrel=1
pkgdesc="A simple profile-based backup script."
arch=(any)
url="https://github.com/DMBuce/blargup"
license=('BSD')
groups=()
depends=('bash')
optdepends=('rsync' 'mysql')
makedepends=('git')
provides=(blargup)
conflicts=(blargup)
replaces=()
backup=('etc/blargup/conf.d/my.cnf'
        'etc/blargup/conf.d/exclude.conf'
        'etc/blargup/systems.d/rsync-pull.sh'
        'etc/blargup/systems.d/rsync-local.sh'
        'etc/blargup/systems.d/mysql.sh'
        'etc/blargup/examples/example-profile')
options=()
install=
source=('git://github.com/DMBuce/blargup.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  sed -i '/^prefix/        s#/usr/local#/usr#
          /^sysconfdir/    s#=.*#= /etc#
          /^localstatedir/ s#=.*#= /var#' Makefile
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  #install -D -m644 etc/examples/example-profile "$pkgdir/etc/blargup/example-profile"
}

# vim:set ts=2 sw=2 et:
