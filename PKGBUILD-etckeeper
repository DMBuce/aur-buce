# Maintainer: Buce <dmbuce@gmail.com>
# Contributor: Danie Roux <accounts@danieroux.com>

pkgname=etckeeper
pkgver=1.17
pkgrel=2
pkgdesc="collection of tools to let /etc be stored in a git, hg or bzr repository."
arch=('any')
backup=('etc/etckeeper/etckeeper.conf')
url="http://kitenet.net/~joey/code/etckeeper/"
license=('GPL2')
#provides=('etckeeper')
makedepends=()
depends=('git' 'inetutils')
source=("$pkgname::git://git.kitenet.net/etckeeper#tag=$pkgver"
        "etckeeper.install"
        "etckeeper.service"
        "etckeeper.timer"
)
md5sums=('SKIP'
         'add2ee5e8aac544ed1e1b18687f1f633'
         '6e9185a355d6c4befa58a12137b2f57a'
         '5efb54d4234ce9d3daad0a5576732a2e'
)

install='etckeeper.install'

prepare() {
  cd "$srcdir/$pkgname"
  sed -i '/^LOWLEVEL_PACKAGE_MANAGER=/ s/=.*/=pacman/' etckeeper.conf
}

#build() {
#  cd "$srcdir/$pkgname"
#  make
#}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/etc/apt" "$pkgdir/etc/cruft"

	install -D -m644 "$srcdir/etckeeper.service" "$pkgdir/usr/lib/systemd/system/etckeeper.service"
	install -D -m644 "$srcdir/etckeeper.timer" "$pkgdir/usr/lib/systemd/system/etckeeper.timer"
}

# vim:set ts=2 sw=2 et:
