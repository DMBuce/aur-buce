# Maintainer: Buce <dmbuce@gmail.com>

pkgname=mmat-git
pkgver=0.25.69adce0
pkgver() {
  cd "$srcdir/$pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always )"
}
pkgrel=1
pkgdesc="Delete unecessary minecraft chunks."
arch=(any)
url="https://github.com/hwei/Minecraft-Map-Auto-Trim"
license=('Apache')
depends=('java-environment' 'bash')
makedepends=('git' 'findutils' 'java-environment')
provides=(mmat)
conflicts=(mmat)
replaces=()
source=(mmat.sh
        "$pkgname::git://github.com/hwei/Minecraft-Map-Auto-Trim.git")
md5sums=('ed92d60335419092a924ed3dbac96870'
         SKIP)

build() {
  cd "$srcdir/$pkgname/src"
  javac $(find . -name \*.java)
  jar -cvfe "../mmat.jar" me.hwei.mctool.MapAutoTrim $(find . -name \*.class)
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m644 mmat.jar "$pkgdir/usr/share/mmat/mmat.jar"
  install -D -m755 "$srcdir/mmat.sh" "$pkgdir/usr/bin/mmat"
}

# vim:set ts=2 sw=2 et:
