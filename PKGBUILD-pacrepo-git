# Maintainer: Buce <dmbuce@gmail.com>

pkgname=pacrepo-git
pkgver=0.30.11c32bd
pkgrel=1
pkgdesc="A simple pacman repository management script"
arch=(any)
url="https://github.com/DMBuce/pacrepo"
license=('BSD')
groups=()
depends=('openssh' 'pacman' 'bash')
makedepends=('git')
provides=(pacrepo)
conflicts=(pacrepo)
replaces=()
backup=('etc/pacrepo.conf')
options=()
install=
source=('git://github.com/DMBuce/pacrepo.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  echo "0.$(git rev-list --count HEAD).$(git describe --always )"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  sed -i '/^prefix/        s#/usr/local#/usr#
          /^sysconfdir/    s#=.*#= /etc#
          /^localstatedir/ s#=.*#= /var#' Makefile
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
